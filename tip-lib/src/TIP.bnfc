comment ";";
Start. Start ::= [Decl];

[].  [Decl] ::= "(" "check-sat" ")";
(:). [Decl] ::= "(" Decl ")"  [Decl];

DeclareDatatypes. Decl ::= "declare-datatypes" "(" [Symbol] ")" "(" [Datatype] ")";
DeclareSort.      Decl ::= "declare-sort" Symbol [Attr] Integer;
DeclareConst.     Decl ::= "declare-const" ConstDecl;
DeclareConstPar.  Decl ::= "declare-const" "(" Par "(" ConstDecl ")" ")";
DeclareFun.       Decl ::= "declare-fun" FunDecl;
DeclareFunPar.    Decl ::= "declare-fun" "(" Par "(" FunDecl ")" ")";
DefineFun.        Decl ::= "define-fun" FunDef;
DefineFunPar.     Decl ::= "define-fun" "(" Par "(" FunDef ")" ")";
DefineFunRec.     Decl ::= "define-fun-rec" FunDef;
DefineFunRecPar.  Decl ::= "define-fun-rec" "(" Par "(" FunDef ")" ")";
DefineFunsRec.    Decl ::= "define-funs-rec" "(" [FunDec] ")" "(" [Expr] ")";
Assert.           Decl ::= Assertion [Attr] Expr;
AssertPar.        Decl ::= Assertion [Attr] "(" Par Expr ")";

AssertIt.  Assertion ::= "assert";
AssertNot. Assertion ::= "assert-not";

Par.         Par    ::= "par" "(" [Symbol] ")";

ConstDecl.   ConstDecl ::= Symbol [Attr] Type;

FunDecl.     FunDecl ::= Symbol [Attr] "(" [Type] ")" Type;

FunDef.      FunDef ::= Symbol [Attr] "(" [Binding] ")" Type Expr;

ParFunDec.   FunDec ::= "(" Par InnerFunDec ")";
MonoFunDec.  FunDec ::= InnerFunDec;
InnerFunDec. InnerFunDec ::= "(" Symbol [Attr] "(" [Binding] ")" Type ")";

Datatype.     Datatype ::= "(" Symbol [Attr] [Constructor] ")";
Constructor.  Constructor ::= "(" Symbol [Attr] [Binding] ")";

Binding. Binding ::= "(" Symbol Type ")";

LetDecl. LetDecl ::= "(" Symbol Expr ")";

TyVar.   Type ::= Symbol;
TyApp.   Type ::= "(" Symbol [Type] ")";
ArrowTy. Type ::= "(" "=>" [Type] ")";
IntTy.   Type ::= "Int";
RealTy.  Type ::= "Real";
BoolTy.  Type ::= "Bool";

Var.       Expr ::= Symbol;
As.        Expr ::= "(" "as" Expr Type ")";
App.       Expr ::= "(" Head [Expr] ")";
Match.     Expr ::= "(" "match" Expr [Case] ")";
Let.       Expr ::= "(" "let" "(" [LetDecl] ")" Expr ")";
Binder.    Expr ::= "(" Binder "(" [Binding] ")" Expr ")";
Lit.       Expr ::= Lit;

LitInt.    Lit ::= Integer;
LitNegInt. Lit ::= "-" Integer;
LitTrue.   Lit ::= "true";
LitFalse.  Lit ::= "false";

Lambda. Binder ::= "lambda";
Forall. Binder ::= "forall";
Exists. Binder ::= "exists";

Case.    Case ::= "(" "case" Pattern Expr ")";

Default.    Pattern ::= "default";
ConPat.     Pattern ::= "(" Symbol [Symbol] ")";
SimplePat.  Pattern ::= Symbol;
LitPat.     Pattern ::= Lit;

Const.      Head ::= Symbol;
At.         Head ::= "@";
IfThenElse. Head ::= "ite";
And.        Head ::= "and";
Or.         Head ::= "or";
Not.        Head ::= "not";
Implies.    Head ::= "=>";
Equal.      Head ::= "=";
Distinct.   Head ::= "distinct";
NumAdd.     Head ::= "+";
NumSub.     Head ::= "-";
NumMul.     Head ::= "*";
NumDiv.     Head ::= "/";
IntDiv.     Head ::= "div";
IntMod.     Head ::= "mod";
NumGt.      Head ::= ">";
NumGe.      Head ::= ">=";
NumLt.      Head ::= "<";
NumLe.      Head ::= "<=";
NumWiden.   Head ::= "to_real";

NoValue. Attr ::= Keyword;
Value.   Attr ::= Keyword Symbol;

terminator LetDecl "";
terminator Case "";
terminator Expr "";
terminator Datatype "";
terminator Constructor "";
terminator Binding "";
terminator Symbol "";
terminator Type "";
terminator FunDecl "";
terminator FunDef "";
terminator FunDec "";
terminator Attr "";

Unquoted. Symbol ::= UnquotedSymbol;
Quoted.   Symbol ::= QuotedSymbol;

position token UnquotedSymbol (letter|["~!@$%^&*_+=<>.?/"])(letter|digit|["~!@$%^&*_-+=<>.?/"])*;
position token QuotedSymbol '|'(char - '|')*'|';
token Keyword ':'(letter|digit|["-"])*;